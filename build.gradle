plugins {
    id "fabric-loom" version "0.5-SNAPSHOT"
	id "maven-publish"
	id 'com.matthewprenger.cursegradle' version "1.4.0"
}

apply plugin: "java"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()

	maven {
		name = "Fabric maven"
		url = "https://maven.fabricmc.net/"
	}

	maven {
		name = "Tehnut maven"
		url "https://maven.tehnut.info"
		content {
			includeGroup "mcp.mobius.waila"
		}
	}

	maven {
		url = "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

    maven {
		url = "https://jitpack.io"
		content {
			includeGroup "com.github.DimensionalDevelopment"
			includeGroup "com.github.Waterpicker"
		}
	}

	maven {
		url = "https://server.bbkr.space/artifactory/libs-release"
		content {
			includeGroup "io.github.cottonmc"
		}
	}

	maven {
		name = "Ladysnake Libs"
		url = "https://dl.bintray.com/ladysnake/libs"
	}
}

def includeCompile(group, artifact, version) {
	project.dependencies {
		modCompileOnly("$group:$artifact:$version") {
			exclude module: "fabric-api"
		}
		modRuntime("$group:$artifact:$version") {
			exclude module: "fabric-api"
		}
		include("$group:$artifact:$version")
	}
	project.publishing {
		publications {
			mavenJava(MavenPublication) {
				pom.withXml {
					def depsNode = asNode().appendNode("dependencies")
					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", group)
					depNode.appendNode("artifactId", artifact)
					depNode.appendNode("version", version)
					depNode.appendNode("scope", "compile")
				}
			}
		}
	}
}

dependencies {
    minecraft "com.mojang:minecraft:1.16.5"
    mappings "net.fabricmc:yarn:1.16.5+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:0.11.0"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.30.0+1.16"
	includeCompile("com.flowpowered", "flow-math", "1.0.3")
	includeCompile("org.jgrapht", "jgrapht-core", "1.1.0")
	includeCompile("com.github.DimensionalDevelopment", "poly2tri.java", "0.1.1")
	includeCompile("com.github.Waterpicker", "OpenWorlds", "c5a1ced")
	includeCompile("io.github.boogiemonster1o1", "libcbe", "1.1.0")
	includeCompile("io.github.cottonmc", "LibGui", "3.3.2+1.16.4")
	includeCompile("me.shedaniel.cloth", "config-2", "4.8.3")
	includeCompile("io.github.onyxstudios", "Cardinal-Components-API", "2.7.10")
	includeCompile("me.sargunvohra.mcmods", "autoconfig1u", "3.3.1")

	modRuntime "curse.maven:Worldedit-225608:3039223" // For saving schematics
	modRuntime "curse.maven:wecui-402098:3169571" // Cos why not

	modCompileOnly "curse.maven:Hwyla-253449:3033613"
	modRuntime "curse.maven:Hwyla-253449:3033613"
    modCompileOnly("io.github.prospector:modmenu:1.14.6+build.31") {
        exclude module: "fabric-api"
    }
    modRuntime("io.github.prospector:modmenu:1.14.6+build.31") {
        exclude module: "fabric-api"
    }
}

version = computeVersion(project.mod_version)
archivesBaseName = "DimensionalDoors"

static def computeVersion(String version) {
	if (version.contains("alpha")) {
		return version + "." + Calendar.getInstance().getTime().format("dd.MM.yyyy")
	}
	return version
}

sourceSets {
	main {
		java {
			srcDir "src/main/schematics"
			srcDir "src/main/registry"
			srcDir "src/main/config"
		}
	}
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    inputs.property "version", project.version
}

artifacts {
    archives jar
}

curseforge {
	if (project.hasProperty('curse_api_key')) {
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '284876'
		changelog = 'This is a test release. Please back up your worlds before loading this mod. A changelog can be found at https://github.com/DimensionalDevelopment/DimDoors/commits/1.17'
		releaseType = 'alpha'
		addGameVersion '1.17'
		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
			displayName = "[${project.minecraft_version}] Dimensional Doors ${version}"
		}
		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}
	options {
		forgeGradleIntegration = false
	}
}

test {
	useJUnitPlatform()
}
